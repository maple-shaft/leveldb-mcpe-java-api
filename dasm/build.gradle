plugins {
	id 'java'
	id 'application'
}

ext {
	generatedSourcesFolder = projectDir.path + '/build/generated-sources/org/middlepath/dassembler'
	dasmG4 = projectDir.path + '/src/main/resources/Dasm.g4'
}

configurations { antlrConfig }

sourceSets {
	main {
		java {
			srcDirs = ['build/generated-sources', 'src/main/java']
		}
		resources { srcDirs = ['src/main/resources'] }
	}
	test {
		java { srcDirs = ['src/test/java'] }
	}
}

repositories { jcenter() }

dependencies {
	compile 'ch.qos.logback:logback-classic:1.3.0-alpha5'
	compile 'org.apache.commons:commons-text:1.6'
	compile 'commons-io:commons-io:2.6'
	compile 'info.picocli:picocli:4.2.0'
	compile 'org.antlr:antlr4:4.7.2'
	antlrConfig 'org.antlr:antlr4:4.7.2'
	testCompile 'junit:junit:4.13'
}

mainClassName = 'org.middlepath.dassembler.DAssemblerMain'

compileJava.configure { dependsOn 'generateDAssembler' }

task generateDAssembler (type: JavaExec) {
	description = 'Generates the new DAssembler Lexer and Parser'
	main = 'org.antlr.v4.Tool'
	classpath = configurations.antlrConfig
	args = [
		'-o',
		generatedSourcesFolder,
		dasmG4
	]
}
	